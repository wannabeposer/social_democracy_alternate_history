title: Military Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: military_policy_timer += 6; month_actions += 1
view-if: spd_in_government = 1 and reichswehr_minister_party = "SPD" and military_policy_timer <= 0
card-image: img/portraits/LeberJulius.jpg
#subtitle: Dealing with the Reichswehr.

= Military Policy

The Reichswehr is a deeply reactionary institution that has been generally hostile to any notion of socialism or democracy. However, we now control the military as part of the government. What should we do to reform it?

- @increase_funding: Increase funding for the military to purchase its loyalty.
- @decrease_funding: Reduce funding for the military so we have more for social spending.
- @reform: Conduct a thorough reform of the ranks, reducing the influence of reactionaries and making it more hospitable through democratic means. 
- @reform_no_funding: Conduct a thorough reform of the ranks, by firing the reacionary officers and soldiers.
- @root: No change to military policy.
- @easy_discard
- @cancel_advisor_action

@increase_funding
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: budget -= 1; coalition_dissent -= 1 if coalition_dissent > 0; reichswehr_loyalty += 0.07 if reichswehr_loyalty < 1; reichswehr_strength += 20; left_dissent += 5; kpd_coalition_dissent += 1; west_relation -= 1; east_relation -= 1; reparations_negotiation -= 1; eu_progress -= 1; economic_growth += 0.1

The military and the center-right parties are pleasantly surprised that we would increase military funding. Our membership tends to be anti-military, and is less enthused. Germany's neighbors are watching our military buildup with worry. [? if reparations >= 0 : This is not good for the reparations negotiations. ?]

@decrease_funding
subtitle: +1 budget
unavailable-subtitle: There is nothing left to cut.
choose-if: reichswehr_strength > 0
on-arrival: budget += 1; coalition_dissent += 1 if in_grand_coalition == 1; dvp_relation -= 8; reichswehr_militancy -= 1; reichswehr_strength -= 20; sh_strength += 15; sa_strength += 5; sh_militancy += 0.05; reichswehr_loyalty -= 0.05 if reichswehr_loyalty > 0; coup_progress += 1; coup_progress += 1 if reichswehr_loyalty < 0.2; coup_progress += 1 if reichswehr_loyalty < 0.1; coup_progress += 1 if reichswehr_loyalty <= 0; reichswehr_goal_completed = 1; economic_growth -= 0.1

The military has fewer guns and fewer soldiers. There is no hint of a coup yet, but we do not know where the line will be drawn. The center-right parties are committed to the military and deeply opposed to our budget cuts.

Many demobilized soldiers have joined the right-wing paramilitaries such as the Stahlhelm and SA, and weapons are missing from our stockpiles.

@reform
subtitle: -1 budget
choose-if: budget >= 1 and reichswehr_loyalty < 1
unavailable-subtitle: [? if budget < 1 : We do not have the budget. ?][? if reichswehr_loyalty >= 1 : The Reichswehr is already fully loyal to the democratic government. ?]
on-arrival: coalition_dissent += 1 if in_grand_coalition == 1; reichswehr_loyalty += 0.25; reichswehr_militancy -= 0.5 if reichswehr_loyalty < 0.5; budget -= 1; coup_progress += 1 if reichswehr_loyalty < 0.25; dvp_relation -= 5 if in_grand_coalition; pacifism += 1; democratization += 1;

Our efforts are at least somewhat successful; we have changed the recruiting and training patterns so that more soldiers from non-reactionary backgrounds can join, hopefully changing the culture of the forces. We have also removed some of the officers who supported the previous Kapp putsch and have provided them incentives to retire.

[? if in_grand_coalition == 1 : The center-right parties are committed to the military and deeply suspicious of our efforts to reform it. ?]

@reform_no_funding
choose-if: (in_weimar_coalition or in_popular_front or in_left_front) and reichswehr_loyalty < 1 and judicial_reform > 3
unavailable-subtitle: [? if in_grand_coalition : Our centre right coalition partners will never allow this. ?][? if reichswehr_loyalty >= 1 : The Reichswehr is already fully loyal to the democratic government. ?] [? if judicial_reform < 3 : The army will fight this move in the courts and the reactionary judiciary will side with them. ?]
on-arrival: coalition_dissent += 2; reichswehr_loyalty += 0.15; reichswehr_militancy -= 0.15 if reichswehr_loyalty < 0.5; coup_progress += 1 if reichswehr_loyalty < 0.5; coup_progress += 1; dvp_relation -= 5; z_relation -=5; ddp_relation -= 5; kpd_relation += 10; sh_strength += 15; sa_strength += 5; sh_militancy += 0.05; reichswehr_goal_completed = 1; pro_republic -= 10; old_middle_dnvp += 30; new_middle_dvp += 20; new_middle_nsdap += 10;

We purge the military and replace them with loyalists. The officers we removed have joined far right paralimitaries. Our bourgeois coalition partners are furious and might call for no-confidence or ask Hindenburg to throw us out. [? if in_popular_front or in_left_front : Our communist allies are pleased with our moves to purge the military. ?]